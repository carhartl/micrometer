plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

dependencies {
    api project(':micrometer-core')

    implementation 'io.projectreactor:reactor-core:latest.release'
    implementation 'io.projectreactor.netty:reactor-netty:latest.release'

    testImplementation project(':micrometer-test')
    testImplementation 'io.projectreactor:reactor-test:latest.release'
    testImplementation 'org.awaitility:awaitility:4.+'
    testImplementation 'ch.qos.logback:logback-classic:1.2.+'
}

shadowJar {
    configurations = [project.configurations.compile]
    classifier = null
    dependencies {
        include(dependency('io.projectreactor:'))
        include(dependency('io.projectreactor.netty:'))
        include(dependency('org.reactivestreams:reactive-streams'))
        include(dependency('io.netty:'))
    }
    relocate 'reactor', 'io.micrometer.shaded.reactor'
    relocate 'org.reactivestreams', 'io.micrometer.shaded.org.reactorstreams'
    relocate 'io.netty', 'io.micrometer.shaded.io.netty'
    exclude 'META-INF/native/libnetty_transport_native_epoll_x86_64.so'
}

jar.enabled = false
jar.dependsOn shadowJar

publishing {
    publications {
        withType(MavenPublication) {
            pom.withXml {
                asNode()
                        .dependencies
                        .dependency
                        .findAll {
                    ['reactor-core', 'reactor-netty'].contains(it.artifactId.text())
                }
                .each { it.parent().remove(it) }
            }
        }
    }
}
